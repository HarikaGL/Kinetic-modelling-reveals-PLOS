#Equations for the network in Fig-3(D), Table 2 (row 5).
#Simulations in Fig-3(E & F)

#	R15:		TC + e5 <----> C1			r15 = (kf15*TC*e5) - (kb15*C1)
#	R16:		TC + e2B <---> C2			r16 = (kf16*TC*e2B) - (kb16*C2)
#	R17:		C2 + e5 <-----> C1 + e2B		r17 = (kf17*C2*e5) - (kb17*C1*e2B)
#	R18:		TC + kin <---> TCkin			r18 = (kf18*TC*kin) - (kb18*TCkin)
#	R19:		TCkin ---------> TCp + kin		r19 = (kf19*TCkin)
#	R20:		TCp + PP <------> TCpPP			r20 = (kf20*TCp*PP) - (kb20*TCpPP)
#	R21:		TCpPP ----------> TC + PP		r21 = (kf21*TCpPP)
#	R22:		TCp + e5 <-----> C3p			r22 = (kf22*TCp*e5) - (kb22*C3p)
#	R23:		TCp + e2B <----> C4p			r23 = (kf23*TCp*e2B) - (kb23*C4p)
#	R24:		C3p + e2B <---> C4p + e5		r24 = (kf24*C3p*e2B) - (kb24*C4p*e5)


				 

# Conservation relations

kin = kint - (TCkin)
PP = PPt - (TCpPP)
e2B = e2Bt - (C2 + C4p)
e5 = e5t - (C1 + C3p)
TC = TCt - (C1 + C2 + C3p + C4p + TCkin + TCp + TCpPP)




# Rate of reactions

######### Module 2 ################

r15 = (kf15*TC*e5) - (kb15*C1)
r16 = (kf16*TC*e2B) - (kb16*C2)
r17 = (kf17*C2*e5) - (kb17*C1*e2B)
r18 = (kf18*TC*kin) - (kb18*TCkin)
r19 = (kf19*TCkin)
r20 = (kf20*TCp*PP) - (kb20*TCpPP)
r21 = (kf21*TCpPP)
r22 = (kf22*TCp*e5) - (kb22*C3p)
r23 = (kf23*TCp*e2B) - (kb23*C4p)
r24 = (kf24*C3p*e2B) - (kb24*C4p*e5)


#Parameters 


param kint=0, e2Bt=18, PPt=4, e5t=40, TCt=50



param kf15= 0.06
param kb15 =	1.19315
param kf16=	15.1547
param kb16=	673.581
param kf17 =	1139.9
param kb17 =	0.384145
param kf18 =	4.33712
param kb18 =	6.51616
param kf19 =	6.78319
param kf20 =	3.22942
param kb20 =	0.0183374
param kf21 =	71.839
param kf22 =	13.9358
param kb22 =	0.717936
param kf23=	6
param kb23=	100
param kf24 =	6457.02
param kb24 =	114.886




# ODE's 

TCp'= r19-r20-r22-r23
C1' = r15+r17
C2'= r16-r17
TCkin'= r18-r19
TCpPP' = r20-r21
C3p'= r22-r24
C4p'= r23+r24


#auxiliaries
aux kin=kin
aux e2B=e2B
aux TC=TC
aux e5=e5
aux PP=PP

aux kintotal = kin+ (TCkin)
aux PPtotal = PP + (TCpPP)
aux e2Btotal = e2B + (C2 + C4p)
aux e5total = e5 + (C1 + C3p)
aux TCtotal = TC + (C1 + C2 + C3p + C4p + TCkin + TCp + TCpPP)


# XPP settings
@ meth=cvode, tol=1e-3,atol=1e-6, total=2000
@ bounds=10000000000, xp=t,  yp=C1
@ xlo=0, xhi=200, ylo=0, yhi=200

#AUTO settings
@ parmin=-10, parmax=1500
@ autoxmin=0, autoymin=0, autoxmax=2 autoymax=30
@ Nmax=20000, Ds=0.02, dsmin=0.001, dsmax=0.5, Normmax=10000
 

done


	
